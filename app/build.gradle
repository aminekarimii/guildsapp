apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'org.sonarqube'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

kapt {
    correctErrorTypes = true
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sqli.guildes"
        minSdkVersion 15
        // Robolectric doesn't support target 28 and above
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Manifest variables
        manifestPlaceholders = [fabricApiKey: "${customFabricApiKey}"]
        resValue "string", "googleAnalyticsVersion", "${customGoogleAnalyticsApiKey}"
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG_STORE_FILE)
            storePassword DEBUG_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
        lintConfig file("../lint.xml")
    }
    androidExtensions {
        experimental = true
    }

    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$navVersion"
    implementation "android.arch.navigation:navigation-ui-ktx:$navVersion"

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    // Firebase
    implementation "com.google.firebase:firebase-core:${googleAnalyticsVersion}"
    implementation "com.google.android.gms:play-services-analytics:${googleAnalyticsVersion}"
    implementation "com.google.android.gms:play-services-gcm:${googleCloudMessagingVersion}"

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.13.1'
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.8.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Architecture Component
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01'

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    // UI
    implementation 'com.airbnb.android:epoxy:3.3.0'
    kapt 'com.airbnb.android:epoxy-processor:3.3.0'
    implementation 'com.github.VladimirWrites:Lemniscate:1.4.5'

    // Unit testing
    testImplementation "org.mockito:mockito-core:2.21.0"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.8"

    // Interface testing
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test:rules:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
}

sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "admin"
        property "sonar.jdbc.password", "admin"
        property "sonar.projectName", "Guildes App"
        property "sonar.projectKey", "com.sqli.guildes"
        property "sonar.projectVersion", android.defaultConfig.versionName
        property "sonar.language", "kotlin"
        property "sonar.profile", "Android Lint"
        property "sonar.android.lint.report", "build/outputs/lint-results"
    }
}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
